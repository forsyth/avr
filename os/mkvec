#!/bin/rc
awk '
BEGIN {NVEC=35}
/^#/	{next;}
NF == 3 {
	n = $1-1
	if(n <= 0 || n >= NVEC){
		print "illegal vector"
		next
	}
	vector[n] = $2
	text[n] = $3
}
END {
	printf "TEXT	_vectors(SB), $-1\n"
	printf "	VECTOR	_startup(SB)\n"
	for(i=1; i<NVEC; i++){
		if(vector[i] == "")
			printf "	VECTOR	_strayintr(SB)\n"	# could use CALL
		else
			printf "	VECTOR	_intr%s(SB)\n", vector[i]
	}
	printf "TEXT	_strayintr(SB), $-1\n"
	printf "	SAVE\n"
	printf "	CALL	strayintr(SB)\n"
	printf "	RESTORE\n"
	printf "	RETI\n"

	for(i=1; i<NVEC; i++)
		if(vector[i] != "" && text[i] == "1"){
			printf "TEXT	_intr%s(SB), $-1\n", vector[i]
			printf "	SAVE\n"
			printf "	CALL	%s(SB)\n", vector[i]
			printf "	RESTORE\n"
			printf "	RETI\n"
		}
}'
